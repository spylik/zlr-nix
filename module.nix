{config, pkgs, ...}:

let 
    # we going to build zlr package from package.nix
    zlr = pkgs.callPackage ./package.nix {};
in

{
    # open port
    networking.firewall.allowedTCPPorts = [80];

    # Create separate group for zalora web service
    users.extraGroups.zlr = {};

    # Create separate user account for zalora web service
    users.extraUsers.zlr = {
        description = "Zalora web service system user";
        home = "/var/log/zlr";
        extraGroups = [
            "zlr"
        ];
        createHome = true;
    };

    # creating system.d service for zalora web service
    systemd.services.zaloraWWW = {
        path = [
            pkgs.gawk               # startup script generated by relx requered awk
            pkgs.gnused             # startup script generated by relx required sed
            pkgs.gzip               # startup script generated by relx required gzip (to archive/unarchive versions for hot-code reload)
            pkgs.utillinux          # startup script generated by relx required logger what in utillinux package
        ];

        description = "Zalora web service.";
        
        # we put service to multi-user.target it is kind of 3rd runlevel (multi-user, non-graphical)
        wantedBy = [ "multi-user.target" ];

        serviceConfig = {
            AmbientCapabilities = "cap_net_bind_service";   # allow it to bind to port less than 1024
            User = "zlr";                                   # running by zlr user
            Group = "zlr";                                  # running by group
            ExecStart = ''${zlr}/bin/zlr foreground'';      # start script (for default - need forking, for foreground - simple type)
            Type = "simple";                                # since we switched for foreground we able to use simple type
        };

        # we will run this service after systemd network.target
        after = [ "network.target" ];

        # enabling service
        enable = true;
    };
}
